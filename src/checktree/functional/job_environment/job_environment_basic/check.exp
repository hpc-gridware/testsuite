#___INFO__MARK_BEGIN__
##########################################################################
#
#  The Contents of this file are made available subject to the terms of
#  the Sun Industry Standards Source License Version 1.2
#
#  Sun Microsystems Inc., March, 2001
#
#
#  Sun Industry Standards Source License Version 1.2
#  =================================================
#  The contents of this file are subject to the Sun Industry Standards
#  Source License Version 1.2 (the "License"); You may not use this file
#  except in compliance with the License. You may obtain a copy of the
#  License at http://gridengine.sunsource.net/Gridengine_SISSL_license.html
#
#  Software provided under this License is provided on an "AS IS" basis,
#  WITHOUT WARRANTY OF ANY KIND, EITHER EXPRESSED OR IMPLIED, INCLUDING,
#  WITHOUT LIMITATION, WARRANTIES THAT THE SOFTWARE IS FREE OF DEFECTS,
#  MERCHANTABLE, FIT FOR A PARTICULAR PURPOSE, OR NON-INFRINGING.
#  See the License for the specific provisions governing your rights and
#  obligations concerning the Software.
#
#  The Initial Developer of the Original Code is: Sun Microsystems, Inc.
#
#  Copyright: 2001 by Sun Microsystems, Inc.
#
#  All Rights Reserved.
#
#  Portions of this software are Copyright (c) 2023-2026 HPC-Gridware GmbH
#
##########################################################################
#___INFO__MARK_END__

# define global variable in this namespace
global check_name
global check_category
global check_description
global check_needs
global check_functions
global check_root_access_needs

# set check_root_access_needs "yes"

# define test's name and run level descriptions
set check_name            "job_environment_basic"
set check_category        "COMPATIBILITY SYSTEM VERIFIED"
set check_description(0)  "test job environment variables"

# define test's dependencies
set check_needs           "init_core_system display_test"

# setup and cleanup functions
set check_setup_function "job_environment_basic_setup"
set check_cleanup_function "job_environment_basic_cleanup"

# define test's procedure order
set check_functions {}
lappend check_functions "job_environment_basic_amount"
lappend check_functions "job_environment_basic_JOB_NAME"
lappend check_functions "job_environment_basic_amount_uninherited"
lappend check_functions "job_environment_basic_JOB_NAME_uninherited"
lappend check_functions "job_environment_basic_ld_preload"
lappend check_functions "job_environment_multi_line"                    ;# CS-1749

# -------- local test procedures -----------------------------------------------

####### job_environment_basic_setup() ################################################
#  NAME
#     job_environment_basic_setup() -- setup the job env test
#
#  SYNOPSIS
#     job_environment_basic_setup { }
#
#  FUNCTION
#     Do required setup for the job environment test, including running the 6.0
#     setup routine if appropriate.
#
#  RESULT
#     0 -> ok   1 -> error
#
#  SEE ALSO
#     job_environment_basic_setup_60()
################################################################################
proc job_environment_basic_setup {} {
   global ts_config
   global job_environment_basic_global_config
   global job_environment_basic_exec_host
   global job_environment_basic_queue
   global job_environment_basic_config
   global host_config_backup

   # increase slots capacity
   foreach host $ts_config(execd_nodes) {
      get_exechost host_config_bkup $host
      set host_config_backup($host) $host_config_bkup(complex_values)

      set host_cfg(complex_values)  "slots=5000"
      set_exechost host_cfg $host
   }

   set job_environment_basic_exec_host [host_conf_get_suited_hosts 1]
   set job_environment_basic_queue [get_queue_instance "all.q" $job_environment_basic_exec_host]

   get_config job_environment_basic_config $job_environment_basic_exec_host

   # allow setting LD_LIBRARY_PATH
   get_config job_environment_basic_global_config
   set conf(qmaster_params) "ENABLE_SUBMIT_LIB_PATH=TRUE"
   set_config conf
}

####### job_environment_basic_cleanup() ##############################################
#  NAME
#     job_environment_basic_cleanup() -- clean up after the job env test
#
#  SYNOPSIS
#     job_environment_basic_cleanup { }
#
#  FUNCTION
#     Clean up changes made during the job environment test.  This function
#     calls the 6.0 cleanup function if appropriate.
#
#  RESULT
#     0 -> ok   1 -> error
#
#  SEE ALSO
#     job_environment_basic_cleanup_60()
################################################################################
proc job_environment_basic_cleanup {} {
   global ts_config
   global job_environment_basic_global_config
   global job_environment_basic_config
   global job_environment_basic_exec_host
   global job_environment_basic_queue
   global host_config_backup

   # restore complex_values
   foreach host $ts_config(execd_nodes) {
      set host_cfg(complex_values) $host_config_backup($host)
      set_exechost host_cfg $host
   }

   ts_log_fine "cleanup ..."
   delete_all_jobs
   wait_for_end_of_all_jobs

   reset_config_and_propagate job_environment_basic_config $job_environment_basic_exec_host

   reset_config job_environment_basic_global_config

   unset -nocomplain job_environment_basic_global_config job_environment_basic_config
   unset -nocomplain job_environment_basic_exec_host job_environment_basic_queue
}

####### job_environment_basic_parse_env_test_output() ################################
#  NAME
#     job_environment_basic_parse_env_test_output() -- parse jobenv.sh output
#
#  SYNOPSIS
#     job_environment_basic_parse_env_test_output { file array }
#
#  FUNCTION
#     Parses the output file from the jobenv.sh script and places the results
#     in the given array.  This function will wait until the output file is
#     available if it does not exist at the time this function is run.
#
#  INPUTS
#     file     the name of the jobenv.sh output file
#     array    the name of the array in which results will be placed
#
#  RESULT
#     0 -> ok   1 -> error
################################################################################
proc job_environment_basic_parse_env_test_output {file array} {
   global CHECK_USER
   global ts_config

   upvar $array job_env_array

   if {[info exists job_env_array]} {
      unset job_env_array
   }

   ts_log_fine "waiting for file $file on host $ts_config(master_host) as user $CHECK_USER ..."
   if {[wait_for_remote_file $ts_config(master_host) $CHECK_USER $file] == 0} {
      get_file_content $ts_config(master_host) $CHECK_USER $file file_array
      set variable ""
      for {set index 1} {$index <= $file_array(0)} {incr index 1} {
         set line $file_array($index)
         set pos [string first "=" $line]
         # this is a line with a new variable
         if {$pos > 0} {
            incr pos -1
            set variable [string range $line 0 $pos]
            incr pos 2
            set value [string range $line $pos end]
            ts_log_finest "variable=\"$variable\", value=\"$value\""
            set job_env_array($variable) $value
            lappend variable_names_list $variable
         } else {
            # this is an additional line for the previous variable
            if {$variable ne ""} {
               set old_value $job_env_array($variable)
               set new_value "$old_value\n$line"
               set job_env_array($variable) $new_value
            }
         }
      }
      set job_env_array(index) $variable_names_list
      delete_remote_file $ts_config(master_host) $CHECK_USER $file
   }
}

####### job_environment_basic_qsub_job() #############################################
#  NAME
#     job_environment_basic_qsub_job() -- qsub jobenv.sh
#
#  SYNOPSIS
#     job_environment_basic_qsub_job { jobname array arguments }
#
#  FUNCTION
#     Submit the jobenv.sh script via qsub and parse the resulting output file.
#
#  INPUTS
#     jobname     the name the job will have when submitted
#     array       the name of the array in which results will be placed
#     arguments   switches to be passed to qsub
#     job_id_var  name of a variable to be used to return the job id (if given)
#     env_var     name of a TCL array containing environment variables to be set for job submission
#
#  RESULT
#     0 -> ok   1 -> error
################################################################################
proc job_environment_basic_qsub_job {jobname array arguments {job_id_var ""} {env_var ""}} {
   global ts_config CHECK_USER
   global CHECK_PROTOCOL_DIR
   global job_environment_basic_queue
   upvar $array job_env_array
   if {$job_id_var ne ""} {
      upvar $job_id_var job_id
   }

   if {$env_var ne ""} {
      upvar $env_var job_env
   }

   set ret 1
   ts_log_fine "job environment should be written to $CHECK_PROTOCOL_DIR"

   set tmp_host [host_conf_get_suited_hosts 1]
   wait_for_remote_file $tmp_host $CHECK_USER "$CHECK_PROTOCOL_DIR/job_env.out" 60 1 1
   set job_id [submit_job "$arguments -q $job_environment_basic_queue $ts_config(testsuite_root_dir)/scripts/jobenv.sh $CHECK_PROTOCOL_DIR" 1 60 $tmp_host "" "" 1 "qsub" 1 "qsub_output" {} "" job_env]

   if {$job_id <= 0} {
      set ret 0
   }

   if {$ret && [wait_for_jobstart $job_id $jobname 120 1 1] != 0} {
      set ret 0
      delete_all_jobs
   }

   if {$ret && [wait_for_jobend $job_id $jobname 120 0 1] != 0} {
      set ret 0
      delete_all_jobs
   }

   wait_for_end_of_all_jobs

   if {$ret} {
      job_environment_basic_parse_env_test_output $CHECK_PROTOCOL_DIR/job_env.out job_env_array
   }

   return $ret
}

####### job_environment_basic_qresub_job() ###########################################
#  NAME
#     job_environment_basic_qresub_job() -- qresub jobenv.sh
#
#  SYNOPSIS
#     job_environment_basic_qresub_job { jobname array arguments }
#
#  FUNCTION
#     Submit the jobenv.sh script via qsub, then again via qresub, and parse the
#     resulting output file.
#
#  INPUTS
#     jobname     the name the job will have when submitted
#     array       the name of the array in which results will be placed
#     arguments   switches to be passed to qsub
#
#  RESULT
#     0 -> ok   1 -> error
################################################################################
proc job_environment_basic_qresub_job { jobname array arguments } {
   global ts_config
   global CHECK_PROTOCOL_DIR CHECK_USER
   global job_environment_basic_queue

   upvar $array job_env_array
   set job_id [submit_job "$arguments -q $job_environment_basic_queue -h $ts_config(testsuite_root_dir)/scripts/jobenv.sh $CHECK_PROTOCOL_DIR"]
   set result [start_sge_bin "qresub" "$job_id"]
   ts_log_fine $result

   set new_job_id [expr $job_id + 1]
   wait_for_jobstart $new_job_id $jobname 60 1 1
   wait_for_jobend $new_job_id $jobname 60 0
   job_environment_basic_parse_env_test_output $CHECK_PROTOCOL_DIR/job_env.out job_env_array
   delete_job $job_id
}

####### job_environment_basic_qalter_job() ###########################################
#  NAME
#     job_environment_basic_qalter_job() -- qalter jobenv.sh
#
#  SYNOPSIS
#     job_environment_basic_qalter_job { jobname array qsub_args qalter_args }
#
#  FUNCTION
#     Submit the jobenv.sh script via qsub in a hold state, change it via
#     qalter, then release is via qrls, and parse the resulting output file.
#
#  INPUTS
#     jobname        the name the job will have when submitted
#     array          the name of the array in which results will be placed
#     qsub_args      switches to be passed to qsub
#     qalter_args    switches to be passed to qalter
#
#  RESULT
#     0 -> ok   1 -> error
################################################################################
proc job_environment_basic_qalter_job { jobname array qsub_args qalter_args } {
   global ts_config
   global CHECK_PROTOCOL_DIR
   global job_environment_basic_queue

   upvar $array job_env_array
   set job_id [submit_job "$qsub_args -q $job_environment_basic_queue -h $ts_config(testsuite_root_dir)/scripts/jobenv.sh $CHECK_PROTOCOL_DIR"]
   set result [start_sge_bin "qalter" "$qalter_args $job_id"]
   ts_log_fine $result

   set result [start_sge_bin "qrls" "$job_id"]
   ts_log_fine $result

   wait_for_jobstart $job_id $jobname 60 1 1
   wait_for_jobend $job_id $jobname 60 0
   job_environment_basic_parse_env_test_output $CHECK_PROTOCOL_DIR/job_env.out job_env_array
}

####### job_environment_basic_qrsh_job() #############################################
#  NAME
#     job_environment_basic_qrsh_job() -- qrsh jobenv.sh
#
#  SYNOPSIS
#     job_environment_basic_qrsh_job { jobname array arguments }
#
#  FUNCTION
#     Submit the jobenv.sh script via qrsh and parse the resulting output file.
#
#  INPUTS
#     array       the name of the array in which results will be placed
#     arguments   switches to be passed to qrsh
#     env_var     name of a TCL array containing environment variables to be set for job submission
#
#  RESULT
#     0 -> ok   1 -> error
################################################################################
proc job_environment_basic_qrsh_job {array arguments {env_var ""}} {
   global ts_config
   global CHECK_PROTOCOL_DIR
   global job_environment_basic_queue

   upvar $array job_env_array

   if {$env_var ne ""} {
      upvar $env_var job_env
   }

   set result [start_sge_bin "qrsh" "$arguments -q $job_environment_basic_queue $ts_config(testsuite_root_dir)/scripts/jobenv.sh $CHECK_PROTOCOL_DIR" "" "" prg_exit_state 60 "" "bin" output_lines job_env]
   ts_log_fine $result

   job_environment_basic_parse_env_test_output $CHECK_PROTOCOL_DIR/job_env.out job_env_array
}

####### job_environment_basic_qsh_job() ##############################################
#  NAME
#     job_environment_basic_qsh_job() -- qsh jobenv.sh
#
#  SYNOPSIS
#     job_environment_basic_qsh_job { array arguments }
#
#  FUNCTION
#     Submit the jobenv.sh script via execution with qsh and parse the resulting
#     output file.
#
#  INPUTS
#     array       the name of the array in which results will be placed
#     arguments   switches to be passed to qsh
#
#  RESULT
#     0 -> ok   1 -> error
################################################################################
proc job_environment_basic_qsh_job { array arguments } {
   global ts_config
   global CHECK_PROTOCOL_DIR CHECK_USER
   global job_environment_basic_exec_host
   global job_environment_basic_queue

   upvar $array job_env_array

   # create a script file to be executed by xterm -e <script> / qsh -S <script>
   set script_name "$CHECK_PROTOCOL_DIR/qsh.sh"
   set f [open $script_name "w" "0755"]
   puts $f "#!/bin/sh"
   puts $f "exec $ts_config(testsuite_root_dir)/scripts/jobenv.sh $CHECK_PROTOCOL_DIR"
   close $f

   # wait for host to appear on the remote host
   wait_for_remote_file $job_environment_basic_exec_host $CHECK_USER $script_name

   set ret 1
   ts_log_fine "starting qsh $arguments -q $job_environment_basic_queue -S $script_name ..."
   set output [start_sge_bin "qsh" "$arguments -q $job_environment_basic_queue -S $script_name" $job_environment_basic_exec_host]
   if {$prg_exit_state != 0} {
      ts_log_severe "starting qsh job failed:\n$output"
      set ret 0
   } else {
      ts_log_fine $output

      # check results
      job_environment_basic_parse_env_test_output $CHECK_PROTOCOL_DIR/job_env.out job_env_array
   }

   # cleanup
   file delete $script_name

   return $ret
}

#****** job_environment_basic_amount() ***********************************************
#  NAME
#     job_environment_basic_amount() -- test job environment
#
#  SYNOPSIS
#     job_environment_basic_amount { }
#
#  FUNCTION
#     Check that all required environment variables are set by the shepherd
#
#  RESULT
#     0 -> ok   1 -> error
#*******************************************************************************
proc job_environment_basic_amount {} {
   global job_environment_basic_exec_host
   set errors ""

   ts_log_fine "Testing list of environment variables with execd_param=none"

   # Set exced_params=none
   # Really want to set to none?
   # We might have necessary params (but probably not in a local config).
   set new_config(execd_params) "none"
   set_config_and_propagate new_config $job_environment_basic_exec_host

   job_environment_basic_var_test missing

   if {[llength $missing] != 0} {
      set missing_vars ""

      foreach missing_var $missing {
         append missing_vars " $missing_var"
      }

      ts_log_severe "The following environment variables should have been set but were not, with execd_params=none: [string trim $missing_vars]"
   }
}

####### job_environment_basic_var_test() #############################################
#  NAME
#     job_environment_basic_var_test() -- test job environment
#
#  SYNOPSIS
#     job_environment_basic_var_test { missing {extra ""} }
#
#  FUNCTION
#     Check that all required environment variables are set by the shepherd.
#     Variables which should be present but are not, are placed in the missing
#     array.  If the extra array is given, variables which are unrecognised are
#     placed in the extra array.
#
#  INPUTS
#     missing     the name of the array in which to place missing env vars
#     extra       the name of the array in which to place extra env vars
#
#  RESULT
#     0 -> ok   1 -> error
################################################################################
proc job_environment_basic_var_test {missing {extra ""}} {
   global ts_config
   global env
   upvar $missing missing_variables
   upvar $extra extra_variables

   set missing_variables ""
   set extra_variables ""

   set known_variables ""
   lappend known_variables "SGE_ROOT"
   lappend known_variables "SGE_CELL"
   lappend known_variables "SGE_ARCH"
   lappend known_variables "SGE_O_HOME"
   lappend known_variables "SGE_O_HOST"
   lappend known_variables "SGE_O_LOGNAME"
   lappend known_variables "SGE_O_PATH"
   lappend known_variables "SGE_O_SHELL"

   # JG: TZ is only set by N1GE, if it is available in the
   # sge_execd's environment.
   # So if we want to check for existence of SGE_O_TZ, we would have
   # to check first, if TZ is set on the exec host (root shell).
   #if { [ info exists env(TZ)] } {
   #   lappend known_variables "SGE_O_TZ"
   #}
   lappend known_variables "SGE_O_WORKDIR"
   lappend known_variables "ARC"
   lappend known_variables "SGE_ACCOUNT"
   lappend known_variables "SGE_CKPT_ENV"
   lappend known_variables "SGE_CKPT_DIR"
   lappend known_variables "SGE_CWD_PATH"
   lappend known_variables "SGE_STDERR_PATH"
   lappend known_variables "SGE_STDOUT_PATH"
   lappend known_variables "SGE_BINARY_PATH"
   lappend known_variables "SGE_JOB_SPOOL_DIR"
   lappend known_variables "SGE_TASK_ID"
   lappend known_variables "SGE_TASK_FIRST"
   lappend known_variables "SGE_TASK_LAST"
   lappend known_variables "SGE_TASK_STEPSIZE"
   lappend known_variables "ENVIRONMENT"
   lappend known_variables "HOME"
   lappend known_variables "HOSTNAME"
   lappend known_variables "JOB_ID"
   lappend known_variables "JOB_NAME"
   lappend known_variables "JOB_SCRIPT"
   lappend known_variables "LOGNAME"
   lappend known_variables "NQUEUES"
   lappend known_variables "NSLOTS"
   lappend known_variables "NHOSTS"
   lappend known_variables "PATH"
   lappend known_variables "PE"
   lappend known_variables "PE_HOSTFILE"
   lappend known_variables "QUEUE"
   lappend known_variables "REQUEST"
   lappend known_variables "SHELL"
   lappend known_variables "TMPDIR"
   lappend known_variables "TMP"
   lappend known_variables "USER"
   lappend known_variables "RESTARTED"

   lappend known_variables "SGE_STDIN_PATH"
   lappend known_variables "SGE_QMASTER_PORT"
   lappend known_variables "SGE_EXECD_PORT"
   if {[is_version_in_range "9.1.0"]} {
      lappend known_variables "SGE_BINDING_TOPOLOGY"
      lappend known_variables "SGE_BINDING_CPUSET"
      lappend known_variables "SGE_BINDING_INSTANCE"
   }

   # Build list of allowed_variables
   set allowed_variables "TZ"
   lappend allowed_variables "SGE_O_TZ"
   lappend allowed_variables "REQNAME"
   # the following might are set by many GE engineers shell rc files
   lappend allowed_variables "ARCH"
   lappend allowed_variables "TERM"
   lappend allowed_variables "PWD"
   # SGE_O_MAIL is only set when mail is correctly configured on the submit host
   lappend allowed_variables "SGE_O_MAIL"

   ts_log_fine "checking number of environment variables ..."

   if {[job_environment_basic_qsub_job "jobenv.sh" job_env "-o /dev/null -e /dev/null -ckpt testcheckpointobject -pe mytestpe 2"]} {
      foreach var $known_variables {
         if {[info exists job_env($var)]} {
            set value $job_env($var)

            if {[string length $value] == 0} {
               lappend missing_variables $var
            }

            # Remove each known_variable from job_env
            array unset job_env $var
         } else {
            lappend missing_variables $var
         }
      }

      # If doing exclusive test...
      if {$extra != ""} {
         # Remove each allowed_variable from job_env
         foreach var $allowed_variables {
            if {[info exists job_env($var)]} {
               if {[string length $job_env($var)] != 0} {
                  array unset job_env $var
               }
            }
         }

         # Test if job_env contains any extra variables
         foreach var [array names job_env] {
            if {[string compare $var "index"] != 0} {
                lappend extra_variables $var
            }
         }
      }
   }
}

####### job_environment_basic_check_env_value() ######################################
#  NAME
#     job_environment_basic_check_env_value() -- test an env var value
#
#  SYNOPSIS
#     job_environment_basic_check_env_value { env_name should_be is_name }
#
#  FUNCTION
#     The value of the environment value is tested against the expected value.
#
#  INPUTS
#     env_name    the name of the environment variable
#     should_be   the value of the environment variable should have
#     is_name     the name of the variable containing the actual value of the
#                 environment variable
#
#  RESULT
#     0 -> ok   1 -> error
################################################################################
proc job_environment_basic_check_env_value {env_name should_be is_name} {
   global ts_config

   upvar $is_name is

   ts_log_fine "checking environment \"$env_name\" ..."

   if {![info exists is]} {
      ts_log_severe "environment variable \"$env_name\" is not set"
   } else {
      set had_error 0
      if {[ string compare $should_be $is] != 0} {
         set had_error 1
         ts_log_fine " failed."
      } else {
         ts_log_fine " ok."
      }
      ts_log_fine " ($env_name=$is)"
      if {$had_error == 1} {
         ts_log_severe "environment variable \"$env_name\" is set to \"$is\", should be \"$should_be\""
      }
   }
}

#****** job_environment_basic_JOB_NAME() *********************************************
#  NAME
#     job_environment_basic_JOB_NAME() -- test the job name env vars
#
#  SYNOPSIS
#     job_environment_basic_JOB_NAME { {stand_alone 1} }
#
#  FUNCTION
#     Tests whether the shepherd correctly sets the job name environment
#     variables
#
#  INPUTS
#     stand_alone    whether this test is running directly or being called from
#                    another test
#
#  RESULT
#     0 -> ok   1 -> error
#*******************************************************************************
proc job_environment_basic_JOB_NAME {{stand_alone 1}} {
   global ts_config CHECK_PROTOCOL_DIR job_environment_basic_exec_host

   if {$stand_alone == 1} {
      ts_log_fine "Testing presence of job env vars with execd_params=none"

      # Set exced_params=none
      set new_config(execd_params) "none"
      set_config_and_propagate new_config $job_environment_basic_exec_host
   }

   ts_log_fine "\nqsub ..."
   if {[job_environment_basic_qsub_job "jobenv.sh" job_env "-o /dev/null -e /dev/null"]} {
      job_environment_basic_check_env_value "JOB_NAME" "jobenv.sh" job_env(JOB_NAME)
      job_environment_basic_check_env_value "REQNAME" "jobenv.sh" job_env(REQNAME)
      job_environment_basic_check_env_value "REQUEST" "jobenv.sh" job_env(REQUEST)
   }

   ts_log_fine "\nqsub -N ..."
   if {[job_environment_basic_qsub_job "newnam" job_env "-o /dev/null -e /dev/null -N newnam"]} {
      job_environment_basic_check_env_value "JOB_NAME" "newnam" job_env(JOB_NAME)
      job_environment_basic_check_env_value "REQNAME" "newnam" job_env(REQNAME)
      job_environment_basic_check_env_value "REQUEST" "newnam" job_env(REQUEST)
   }

   ts_log_fine "\nqresub ..."
   job_environment_basic_qresub_job "jobenv.sh" job_env "-o /dev/null -e /dev/null"
   job_environment_basic_check_env_value "JOB_NAME" "jobenv.sh" job_env(JOB_NAME)
   job_environment_basic_check_env_value "REQNAME" "jobenv.sh" job_env(REQNAME)
   job_environment_basic_check_env_value "REQUEST" "jobenv.sh" job_env(REQUEST)

   ts_log_fine "\nqalter ..."
   job_environment_basic_qalter_job "newnam" job_env "-o /dev/null -e /dev/null" "-N newnam"
   job_environment_basic_check_env_value "JOB_NAME" "newnam" job_env(JOB_NAME)
   job_environment_basic_check_env_value "REQNAME" "newnam" job_env(REQNAME)
   job_environment_basic_check_env_value "REQUEST" "newnam" job_env(REQUEST)

   ts_log_fine "\nqsh ..."
   if {[job_environment_basic_qsh_job job_env ""]} {
      job_environment_basic_check_env_value "JOB_NAME" "INTERACTIVE" job_env(JOB_NAME)
      job_environment_basic_check_env_value "REQNAME" "INTERACTIVE" job_env(REQNAME)
      job_environment_basic_check_env_value "REQUEST" "INTERACTIVE" job_env(REQUEST)
   }

#   can't test qlogin, need password to login !!!
#   ts_log_fine "\nqlogin ..."
#   ts_log_fine "job name is \"$job_env(JOB_NAME)\""

   ts_log_fine "\nqrsh ..."
   job_environment_basic_qrsh_job job_env ""
   job_environment_basic_check_env_value "JOB_NAME" "jobenv.sh" job_env(JOB_NAME)
   job_environment_basic_check_env_value "REQNAME" "jobenv.sh" job_env(REQNAME)
   job_environment_basic_check_env_value "REQUEST" "jobenv.sh" job_env(REQUEST)
}



#****** job_environment_basic_JOB_NAME_uninherited() *********************************
#  NAME
#     job_environment_basic_JOB_NAME_uninherited() -- test the job name env vars
#
#  SYNOPSIS
#     job_environment_basic_JOB_NAME_uninherited { }
#
#  FUNCTION
#     Tests whether the shepherd correctly sets the job name environment
#     variables when the execd_param, INHERIT_ENV, is set to false.
#
#  RESULT
#     0 -> ok   1 -> error
#*******************************************************************************
proc job_environment_basic_JOB_NAME_uninherited {} {
   global job_environment_basic_exec_host

   ts_log_fine "Testing presence of job env vars with INHERIT_ENV=false"

   # Set INHERIT_ENV=false
   set new_config(execd_params) "INHERIT_ENV=false"
   set_config_and_propagate new_config $job_environment_basic_exec_host

   # Run JOB_NAME test
   job_environment_basic_JOB_NAME 0
}




#****** job_environment_basic_amount_uninherited() ***********************************
#  NAME
#     job_environment_basic_amount_uninherited() -- test job environment
#
#  SYNOPSIS
#     job_environment_basic_amount_uninherited { }
#
#  FUNCTION
#     Check that all required environment variables are set by the shepherd when
#     the execd_param, INHERIT_ENV, is set to false.
#
#  RESULT
#     0 -> ok   1 -> error
#*******************************************************************************
proc job_environment_basic_amount_uninherited {} {
   global job_environment_basic_exec_host

   ts_log_fine "Testing list of environment variables with INHERIT_ENV=false"

   set new_config(execd_params) "INHERIT_ENV=false"
   set_config_and_propagate new_config $job_environment_basic_exec_host

   set errors ""
   set warnings ""
   job_environment_basic_var_test missing extra
   set error_type "-3"
   if {[llength $missing] != 0} {
      set missing_vars ""

      foreach missing_var $missing {
         append missing_vars " $missing_var"
      }
      append errors "\nThe following environment variables should have been set but were not: [string trim $missing]"
      set error_type "-1"
   } elseif {[llength $extra] != 0} {
      set extra_vars ""
      foreach extra_var $extra {
         append extra_vars " $extra_var"
      }
      append warnings "\nThe following unrecognized environment variables were set: [string trim $extra_vars]"
   }

   if {$errors != ""} {
      ts_log_severe [string trim $errors]
   }
   if {$warnings != ""} {
      ts_log_info [string trim $warnings]
   }
}

proc job_environment_basic_ld_preload {} {
   global ts_config

   # submit a job with LD_PRELOAD set, expect it to be rejected
   # we submit it with hold, it will not be executed
   set job_opts "-o /dev/null -j y -h -b y -v LD_PRELOAD=/tmp/my_bad_lib.so"
   set job_args "sleep 100"
   set job_id [submit_job "$job_opts $job_args" 0]
   ts_log_fine $qsub_output
   set pattern [translate_macro MSG_REMOVED_ENV_VAR_S "LD_PRELOAD"]
   if {$job_id <= 0} {
      ts_log_severe "job should have got submitted, but was rejected:\n$qsub_output"
   } else {
      if {[string first $pattern $qsub_output] < 0} {
         ts_log_severe "LD_PRELOAD was accepted during job submission, but should have been removed:\n$qsub_output"
      }
   }
   delete_job $job_id

   # add ENABLE_SUBMIT_LD_PRELOAD=TRUE to the qmaster params
   get_config global_conf_backup
   set conf(qmaster_params) "$global_conf_backup(qmaster_params),ENABLE_SUBMIT_LD_PRELOAD=TRUE"
   set_config conf
   # submit a job with LD_PRELOAD set, expect it to be accepted
   set job_id [submit_job "$job_opts $job_args" 0]
   ts_log_fine $qsub_output
   if {$job_id <= 0} {
      ts_log_severe "job should have got submitted, but was rejected:\n$qsub_output"
   } else {
      if {[string first $pattern $qsub_output] >= 0} {
         ts_log_severe "LD_PRELOAD was removed, but should have been accepted:\n$qsub_output"
      }
   }
   delete_job $job_id

   # start a job with LD_PRELOAD set, expect LD_PRELOAD to be set in the environment
   # we set LD_PRELOAD to the libdrmaa.so, this should not cause any issues in the job execution
   set host [lindex $ts_config(execd_nodes) 0]
   set arch [resolve_arch $host]
   set libdrmaa "$ts_config(product_root)/lib/$arch/libdrmaa.so"
   set output [start_sge_bin "qrsh" "-l h=$host -v LD_PRELOAD=$libdrmaa env"]
   if {[string first "LD_PRELOAD=$libdrmaa" $output] < 0} {
      ts_log_severe "LD_PRELOAD was not set in the environment:\n$output"
   } else {
      ts_log_fine "LD_PRELOAD was set in the environment"
   }

   # restore global config
   reset_config global_conf_backup
}

proc job_environment_multi_line {} {
   global ts_config

   if {![is_version_in_range "9.0.11 9.1.0"]} {
      ts_log_info "Skipping multi line environment variable test, CS-1749 has only been fixed in 9.0.11 and higher"
      return
   }

   ts_log_fine "Testing multi line environment variable"

   set multiline "line1\nline2\nline3"
   set myenv(TEST) $multiline

   ts_log_fine "\nqsub ..."
   if {[job_environment_basic_qsub_job "jobenv.sh" job_env "-V" "" myenv]} {
      job_environment_basic_check_env_value "TEST" $multiline job_env(TEST)
   }

   ts_log_fine "\nqrsh ..."
   job_environment_basic_qrsh_job job_env "-V" myenv
   job_environment_basic_check_env_value "TEST" $multiline job_env(TEST)
}
