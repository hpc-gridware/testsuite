#___INFO__MARK_BEGIN_NEW__
###########################################################################
#
#  The contents of this file are made available subject to the terms of the
#  Apache Software License 2.0 ('The License').
#  You may not use this file except in compliance with The License.
#  You may obtain a copy of The License at
#  http://www.apache.org/licenses/LICENSE-2.0.html
#
#  Copyright (c) 2011 Univa Corporation.
#
#  Portions of this software are Copyright (c) 2023-2025 HPC-Gridware GmbH
#
###########################################################################
#___INFO__MARK_END_NEW__

# define global variable in this namespace
global check_name
global check_category
global check_description
global check_needs
global check_functions
global check_root_access_needs
global check_need_running_system

#set check_root_access_needs "yes"
#set check_need_running_system "no"

# define test's name and run level descriptions
set check_name            "ge_3306"
set check_category        "VERIFIED COMPATIBILITY SYSTEM SCHEDULER"
set check_description(0)  "jobs get scheduled but are not started after global host changed"

# define test's dependencies
set check_needs           "init_core_system"

# setup and cleanup functions
set check_setup_function ge_3306_setup
set check_cleanup_function ge_3306_cleanup

# define test's procedure order
set check_functions {}
lappend check_functions "ge_3306_test"
lappend check_functions "ge_3306_per_job"
lappend check_functions "ge_3306_exclusive"
lappend check_functions "ge_3306_pe_slots"

# -------- local test procedures: initialization------------------------------

proc ge_3306_setup {} {
   global ts_config
   global ge_3306_complex_backup ge_3306_global_host_backup ge_3306_host_backup ge_3306_host

   # we want to test global resources and on a specific host
   set ge_3306_host [host_conf_get_suited_hosts]

   # we add a consumable - backup the complex definition
   get_complex ge_3306_complex_backup

   # we modify the global and an exec host - do a backup
   get_exechost ge_3306_global_host_backup
   get_exechost ge_3306_host_backup

   # create test consumable
   set test_complex(C1) "C1 INT <= YES YES 0 0"
   set test_complex(CJ) "CJ INT <= YES JOB 0 0"
   set test_complex(CX) "CX BOOL EXCL YES YES 0 0"
   set_complex test_complex

   # add a pe and a queue referencing the pe
   set pe(slots) 1000
   set pe(allocation_rule) "\$round_robin"
   add_pe "ge_3306" pe
   set queue(slots) 1
   set queue(pe_list) "ge_3306"
   add_queue "ge_3306" "@allhosts" queue

   # set a high scheduling interval - we trigger it manually
   set schedd_config(schedule_interval) "0:10:0"
   set schedd_config(flush_submit_sec) "0"
   set schedd_config(flush_finish_sec) "0"
   set schedd_config(schedd_job_info) "true"
   set_schedd_config schedd_config

   # restart qmaster (scheduler) instrumented with delay before scheduling
   shutdown_master_and_scheduler $ts_config(master_host) [get_qmaster_spool_dir]
   set myenv(SGE_TEST_DELAY_SCHEDULING) 5
   startup_qmaster 1 myenv

   # start a tail -f on the qmaster messages file
   init_logfile_wait $ts_config(master_host) "[get_qmaster_spool_dir]/messages"

}

proc ge_3306_cleanup {} {
   global ge_3306_complex_backup ge_3306_global_host_backup ge_3306_host_backup ge_3306_host

   # close our tail -f <qmaster_messages_file>
   close_logfile_wait

   # restart qmaster (scheduler) to get rid of instrumentation
   shutdown_and_restart_qmaster

   # reset the scheduler config
   reset_schedd_config

   # clean up left over jobs
   delete_all_jobs
   wait_for_end_of_all_jobs

   # delete queue and pe
   del_queue "ge_3306" "" 1 1
   del_pe "ge_3306"

   # restore the host backup
   set_exechost ge_3306_global_host_backup
   set_exechost ge_3306_host_backup

   # reset the complex
   reset_complex ge_3306_complex_backup

   # unset our test's global variables
   unset -nocomplain ge_3306_complex_backup ge_3306_global_host_backup ge_3306_host_backup ge_3306_host
}

proc ge_3306_get_pattern {var host slots remaining} {
   # set the pattern for the error message
   if {[is_version_in_range "9.0.0"]} {
      return [translate_macro MSG_CAPACITYEXCEEDED_FSSSIFF "1.0*" $var host "$host*" $slots "0.0*" $remaining]
   } else {
      return [translate_macro MSG_CAPACITYEXCEEDED_FSSSIF "1.0*" $var host "$host*" $slots $remaining]
   }
}

proc ge_3306_delete_jobs {job_ids_var} {
   upvar $job_ids_var job_ids

   ts_log_fine "deleting jobs $job_ids"

   # delete all jobs
   foreach job_id $job_ids {
      delete_job $job_id 1
   }

   # all jobs gone
   set job_ids {}
}

proc ge_3306_do_test {params_var job_ids_var} {
   global ts_config CHECK_USER

   upvar $params_var params
   upvar 1 $job_ids_var job_ids

   # set a start value for our consumable
   mod_attr exechost complex_values "$params(variable)=$params(value_start)" $params(host)

   # submit a job requesting our resource
   set job_args "$params(qsub_options) -b y -o /dev/null -j y"
   set job_script "sleep 1000"
   set job_id [submit_job "$job_args $job_script"]
   if {$job_id <= 0} {
      return 0
   }

   # trigger a scheduling run
   trigger_scheduling

   # modify the consumable value
   mod_attr exechost complex_values "$params(variable)=$params(value_change)" $params(host)

   # check if the job is running
   if {[wait_for_jobstart $job_id "" 20 0] == 0} {
      # job is running
      lappend job_ids $job_id
      # if it shouldn't be running: error
      if {!$params(job_running)} {
         ts_log_severe "job ($params(qsub_options)) did start - consumable check at job delivery does not work"
         set ret 0
      } else {
         set ret 1
      }
   } else {
      # job is not running
      # if it should be running: error
      if {$params(job_running)} {
         ts_log_severe "job ($params(qsub_options)) did not start - GE-3306 is present\n\n\
                        qalter -w p $job_id:\
                        [start_sge_bin "qalter" "-w p $job_id"]\n\n\
                        qstat -F $params(variable):\
                        [start_sge_bin "qstat" "-F $params(variable)"]\n\n\
                        tail <qmaster_spool_dir>/messages:\n\
                        [start_remote_prog $ts_config(master_host) $CHECK_USER tail "[get_qmaster_spool_dir]/messages"]"
         lappend job_ids $job_id
         set ret 0
      } else {
         # output qstat -j job_id with the schedd_job_info, may help in debugging
         ts_log_fine [start_sge_bin "qstat" "-j $job_id"]
         # expect appropriate error message in qmaster messages file
         # error message is generated by logfile_wait
         logfile_wait $params(message) 60 0
         if {$logfile_wait_error == 0} {
            # everything OK, error message found
            set ret 1
            # delete job to avoid it getting scheduled in further scenarios
            delete_job $job_id
         } else {
            lappend job_ids $job_id
            set ret 0
         }
      }
   }

   # in case of error cleanup all jobs
   if {!$ret} {
      ge_3306_delete_jobs job_ids
   }

   return $ret
}

proc ge_3306_test {} {
   global ge_3306_host

   set job_ids {}

   # test global and per host consumable
   foreach host "global $ge_3306_host" {
      # make sure scheduler is idle
      trigger_scheduling ; after 10000

      # start a sequential job, must start
      set params(host) $host
      set params(qsub_options) "-l h=$ge_3306_host -l C1=1"
      set params(variable) "C1"
      set params(value_start) 1
      set params(value_change) 2
      set params(job_running) 1
      if {![ge_3306_do_test params job_ids]} {
         return
      }
      # start a second job, reduce consumable, must get rejected
      set params(value_start) "2"
      set params(value_change) "1"
      set params(job_running) 0
      set params(message) [ge_3306_get_pattern "C1" $host 1 "0.0*"]
      if {![ge_3306_do_test params job_ids]} {
         return
      }
      # start a pe job, expect it to run
      set params(qsub_options) "-l C1=1 -pe mytestpe 2"
      set params(value_start) 3
      set params(value_change) 3
      set params(job_running) 1
      if {![ge_3306_do_test params job_ids]} {
         return
      }
      # start a pe job, expect it to get rejected at start
      set params(value_start) 5
      set params(value_change) 4
      set params(job_running) 0
      set params(message) [ge_3306_get_pattern "C1" $host 2 "1.0*"]
      if {![ge_3306_do_test params job_ids]} {
         return
      }

      # cleanup
      ge_3306_delete_jobs job_ids
      del_attr exechost complex_values $params(variable) $params(host)
   }
}

proc ge_3306_per_job {} {
   global ge_3306_host

   set job_ids {}

   # test global and per host consumable
   foreach host "global $ge_3306_host" {
      # make sure scheduler is idle
      trigger_scheduling ; after 10000

      # start a sequential job, must start
      set params(host) $host
      set params(qsub_options) "-l h=$ge_3306_host -l CJ=1"
      set params(variable) "CJ"
      set params(value_start) 1
      set params(value_change) 2
      set params(job_running) 1
      if {![ge_3306_do_test params job_ids]} {
         return
      }
      # start a second job, reduce consumable, must get rejected
      set params(value_start) "2"
      set params(value_change) "1"
      set params(job_running) 0
      set params(message) [ge_3306_get_pattern "CJ" $host 1 "0.0*"]
      if {![ge_3306_do_test params job_ids]} {
         return
      }
      # start a pe job, expect it to run
      set params(qsub_options) "-l CJ=1 -pe mytestpe 2"
      set params(value_start) 2
      set params(value_change) 2
      set params(job_running) 1
      if {![ge_3306_do_test params job_ids]} {
         return
      }
      # start a pe job, expect it to get rejected at start
      set params(value_start) 3
      set params(value_change) 2
      set params(job_running) 0
      set params(message) [ge_3306_get_pattern "CJ" $host 1 "0.0*"]
      if {![ge_3306_do_test params job_ids]} {
         return
      }

      # cleanup
      ge_3306_delete_jobs job_ids
      del_attr exechost complex_values $params(variable) $params(host)
   }
}

proc ge_3306_exclusive {} {
   global ge_3306_host

   set job_ids {}

   # test global and per host consumable
   foreach host "global $ge_3306_host" {
      # make sure scheduler is idle
      trigger_scheduling ; after 10000

      # start a sequential job, must start
      set params(host) $host
      set params(qsub_options) "-l h=$ge_3306_host -l CX"
      set params(variable) "CX"
      set params(value_start) 1
      set params(value_change) 1
      set params(job_running) 1
      if {![ge_3306_do_test params job_ids]} {
         return
      }
      # cleanup - we can only start one exclusive job
      ge_3306_delete_jobs job_ids

      # make sure scheduler is idle
      trigger_scheduling ; after 10000
      # start a job, reduce consumable, must get rejected
      set params(value_start) 1
      set params(value_change) 0
      set params(job_running) 0
      set params(message) [ge_3306_get_pattern "CX" $host 1 "0.0*"]
      if {![ge_3306_do_test params job_ids]} {
         return
      }

      # cleanup
      ge_3306_delete_jobs job_ids
      del_attr exechost complex_values $params(variable) $params(host)
   }
}

proc ge_3306_pe_slots {} {
   global ge_3306_host

   set job_ids {}

   # make sure scheduler is idle
   trigger_scheduling ; after 10000

   # start a pe job spawning multiple hosts
   # we have only one global license, but it gets
   # requested once for for every host, so job
   # may not get started
   set params(host) "global"
   set params(qsub_options) "-pe ge_3306 2 -l C1=1"
   set params(variable) "C1"
   set params(value_start) 2
   set params(value_change) 1
   set params(job_running) 0
   set params(message) [ge_3306_get_pattern "C1" "global" 2 "1.0*"]
   if {![ge_3306_do_test params job_ids]} {
      return
   }

   # cleanup
   ge_3306_delete_jobs job_ids
   del_attr exechost complex_values $params(variable) $params(host)
}
